#version 430 core
#define M_PI 3.1415926535897932384626433832795
#define M_G 9.81

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;
layout(rgba32f, binding = 0) uniform image2D textureH0;

uniform int gridSize;
uniform int horizontalDimension;
uniform float amplitude;
uniform vec2 windDirection;
uniform float windVelocity;


void main() {
	ivec2 pixel_pos = ivec2(gl_GlobalInvocationID.xy);
	vec2 x = vec2(pixel_pos) - float(gridSize) / 2.0;

	vec2 k = 2 * M_PI * x / horizontalDimension;
	
	
	float L_ = windVelocity * windVelocity / M_G;
	float mag = length(k);

	// prevent division by zero
	if (mag < 0.00001) {
		mag = 0.00001;
	}
	float magSq = mag * mag;
	
	float temp = dot(normalize(k), normalize(windDirection));

	float h0 = clamp(sqrt((amplitude / (magSq * magSq))
			 * temp * temp * temp * temp
			 * exp(-(1.0 / (magSq * L_ * L_)))
			 * exp(-magSq * pow(horizontalDimension / 2000.0, 2.0))) / sqrt(2.0), -4000, 4000);

	float temp2 = dot(normalize(-k), normalize(windDirection));

	float h0inverse = clamp(sqrt((amplitude / (magSq * magSq))
			 * temp2 * temp2 * temp2 * temp2
			 * exp(-(1.0 / (magSq * L_ * L_)))
			 * exp(-magSq * pow(horizontalDimension / 2000.0, 2.0))) / sqrt(2.0), -4000, 4000);

	

	vec4 randomNumbers = imageLoad(textureH0, pixel_pos);

	imageStore(textureH0, pixel_pos, vec4(randomNumbers.xy * h0, randomNumbers.zw * h0inverse));
}